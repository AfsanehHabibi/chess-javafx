plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'info.solidsoft.pitest' version '1.9.0'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.openjfx:javafx-controls:17'
    implementation 'org.openjfx:javafx-fxml:17'
    testImplementation 'junit:junit:4.13.2'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine:5.8.2'
    implementation files('libs/randoop-4.3.2/randoop-all-4.3.2.jar')
}

java {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

mainClassName = 'sample.Main'

task runServerClass(type: JavaExec) {
    main = 'sample.server.Server' // Specify the fully qualified name of your main class here
    classpath = sourceSets.main.runtimeClasspath
}

task runClientClass(type: JavaExec) {
    main = 'sample.Main' // Specify the fully qualified name of your main class here
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ['--module-path', classpath.asPath, '--add-modules', 'javafx.controls,javafx.fxml']
}

run {
    jvmArgs = ['--module-path', classpath.asPath, '--add-modules', 'javafx.controls,javafx.fxml']
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

pitest {
    targetClasses = ['sample.game.logic.*']  //by default "${project.group}.*"
    targetTests = ['*']
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
}